FUNCTION ODBC_SETUP_SCREEN (setuppass)   
   PasswordEntry = CREATEOBJECT("PASSWORDSCR")
   PasswordEntry.PASSWORD = setuppass
   PasswordEntry.Show()
   
   IF PasswordEntry.Success
      RUN /N interface_config.exe
      *OODBCSetupScreen = CREATEOBJECT("ODBCSCR")
      *OODBCSetupScreen.Show()
   ENDIF
   PASSWORDENTRY.RELEASE
ENDFUNC

FUNCTION EXTRACT_SQLSOURCE(HANDLE)
   CNCTSTR = ALLTRIM(SQLGETPROP(HANDLE, "CONNECTSTRING"))
   X1 = AT(';', CNCTSTR, 1)
   XSTRING1 = ALLTRIM(LEFT(CNCTSTR, X1-1))
   XLEN1 = LEN(XSTRING1)
   X11 = AT('=',XSTRING1,1)
   XDATASOURCE = ALLTRIM(RIGHT(XSTRING1, XLEN1 - X11))
   RETURN XDATASOURCE
ENDFUNC

DEFINE CLASS ODBC_SETUP AS Custom
   DIMENSION TABLE_NAMES(1)
   DIMENSION FIELD_NAMES(1)
   SQLSOURCE = ''
   ODBCTABLE = ''
   FUNCTION SET_SOURCE(SQLSOURCE)
      IF EMPTY(SQLSOURCE)
         HANDLE = SQLCONNECT()
      ELSE
         HANDLE = SQLCONNECT(SQLSOURCE)
      ENDIF
      IF HANDLE > 0
         THIS.SQLSOURCE = EXTRACT_SQLSOURCE(HANDLE)
         SQLDISCONNECT(HANDLE)
      ENDIF
   ENDFUNC
   FUNCTION GET_TABLES
      IF EMPTY(THIS.SQLSOURCE)
         RETURN
      ENDIF
      HANDLE = SQLCONNECT(THIS.SQLSOURCE)
      IF HANDLE > 0
         RETVAL = SQLTABLES(HANDLE, 'TABLES', 'TABLESCURSOR')
         IF RETVAL > 0
            TRY
               SELECT TABLENAME FROM TABLESCURSOR INTO ARRAY THIS.TABLE_NAMES
            CATCH
               SELECT TABLE_NAME FROM TABLESCURSOR INTO ARRAY THIS.TABLE_NAMES
            ENDTRY
         ENDIF
         SQLDISCONNECT(HANDLE)
      ENDIF
   ENDFUNC
   FUNCTION GET_FIELDS
      IF EMPTY(THIS.SQLSOURCE)
         RETURN
      ENDIF
      IF EMPTY(THIS.ODBCTABLE)
         RETURN
      ENDIF
      HANDLE = SQLCONNECT(THIS.SQLSOURCE)
      IF HANDLE > 0
         RETVAL = SQLCOLUMNS(HANDLE, ALLTRIM(THIS.ODBCTABLE), 'FOXPRO', 'FIELDSCURSOR')
         IF RETVAL > 0
            SELECT FIELD_NAME FROM FIELDSCURSOR INTO ARRAY THIS.FIELD_NAMES
         ENDIF
         SQLDISCONNECT(HANDLE)
      ENDIF
   ENDFUNC
ENDDEFINE

DEFINE CLASS LIGHTUPTEXTBOX AS TEXTBOX
   FONTBOLD = .T.
   HEIGHT = 25
   FONTNAME = 'TIMES NEW ROMAN'
   FONTSIZE = 10
   WIDTH = 150
   FORMAT = 'K'
   TABSTOP = .T.
   ENABLED = .T.
   VISIBLE = .T.
   PROCEDURE GOTFOCUS
      THIS.BACKCOLOR = RGB(255,255,0)
   ENDPROC
   PROCEDURE LOSTFOCUS
      THIS.BACKCOLOR = RGB(255,255,255)
   ENDPROC
ENDDEFINE

DEFINE CLASS STDLABEL AS Label
   VISIBLE = .T.
   AUTOSIZE =.T.
   FONTNAME = 'TIMES NEW ROMAN'
   FONTSIZE = 10
   FONTBOLD = .T.
   BACKSTYLE = 0
ENDDEFINE

DEFINE CLASS STDBUTTON AS COMMANDBUTTON
   HEIGHT = 30
   WIDTH = 80
   FONTSIZE = 12
   FONTBOLD = .T.
   FONTNAME = 'TIMES NEW ROMAN'
ENDDEFINE

DEFINE CLASS MODALSCR AS FORM
   ICON = 'ams.ico'
   MDIFORM = .F.
   FONTSIZE = 8
   AUTOCENTER = .T.
   BORDERSTYLE = 2
   MINBUTTON = .F.
   MAXBUTTON = .F.
   WINDOWTYPE = 1
   SHOWTIPS = .T.
   PROCEDURE QUERYUNLOAD
      IF THIS.RELEASETYPE=1
         NODEFAULT
      ENDIF
   ENDPROC
ENDDEFINE

DEFINE CLASS ODBCSCR AS MODALSCR
   HEIGHT = 400
   WIDTH = 445
   CAPTION = "Interface Setup"
   ADD OBJECT ODBCCONFIG AS ODBC_SETUP
   
   ADD OBJECT LB_SQLLOGIN AS STDLABEL WITH LEFT = 10, TOP = 8, CAPTION = "SQL Login:"
   ADD OBJECT LB_SQLPASSWORD AS STDLABEL WITH LEFT = 10, TOP = 33, CAPTION = "SQL Password:"  
   ADD OBJECT LB_SQLSOURCE AS STDLABEL WITH LEFT = 10 , TOP = 58, CAPTION = "SQL DataSource:"
   
   ADD OBJECT SQLLOGIN AS LIGHTUPTEXTBOX WITH LEFT = 150, TOP = 5
   ADD OBJECT SQLPASSWORD AS LIGHTUPTEXTBOX WITH LEFT = 150, TOP = 30
   ADD OBJECT SQLSOURCE AS LIGHTUPTEXTBOX WITH LEFT = 150, TOP = 55
      SQLSOURCE.FORMAT = '!K'
   
   ADD OBJECT CMD_SQLSOURCE AS STDBUTTON WITH LEFT = 305, TOP = 58, CAPTION = "..."
       CMD_SQLSOURCE.toolTipText = 'Select SQL DataSource/Login/Password'
       CMD_SQLSOURCE.HEIGHT = 25
       CMD_SQLSOURCE.WIDTH = 25
       CMD_SQLSOURCE.fontsize = 10
       CMD_SQLSOURCE.tabstop = .F.
   
   ADD OBJECT LB_SCREENPASSWORD AS STDLABEL WITH LEFT = 10, TOP = 108, CAPTION = "Void / Duplicate Password:"
   ADD OBJECT SCREENPASSWORD AS LIGHTUPTEXTBOX WITH LEFT = 200, TOP = 105
   
   ADD OBJECT CMD_SAVE AS STDBUTTON WITH LEFT = 275, TOP = 365, CAPTION = "\<Save"
       CMD_SAVE.toolTipText = 'Save Interface Options'
   
   ADD OBJECT cmd_cancel AS STDBUTTON WITH LEFT = 360, TOP = 365, CAPTION = "\<Exit"
       cmd_cancel.toolTipText = 'Exit'
       cmd_cancel.tabstop = .F.
       
   ADD OBJECT TABLE_SELECT AS COMBOBOX WITH LEFT = 10, TOP = 158
       
   *ADD OBJECT LB_SQLFIELDS AS STDLABEL WITH LEFT = 10, TOP = 158, CAPTION = "SQL FIELDS:"
   
   ADD OBJECT field_list AS LISTBOX WITH LEFT = 10, TOP = 183, WIDTH = 200, HEIGHT = 177
       FIELD_LIST.TABSTOP = .F.

   ADD OBJECT LB_SELECTED_FIELDS AS STDLABEL WITH LEFT = 235, TOP = 158, CAPTION = "SELECTED FIELDS:"
   ADD OBJECT selected_field_list AS LISTBOX WITH LEFT = 235, TOP = 183, WIDTH = 200, HEIGHT = 177
       SELECTED_FIELD_LIST.TABSTOP = .F.
   
   PROCEDURE INIT
      IF FILE('ODBC_SETUP.DBF')
         USE ODBC_SETUP IN 0 SHARED
         SELECT ODBC_SETUP
         
         THISFORM.SQLLOGIN.VALUE = ALLTRIM(ODBC_SETUP.SQL_LOGIN)
         THISFORM.SQLPASSWORD.VALUE = ALLTRIM(ODBC_SETUP.SQL_PASWRD)
         THISFORM.SQLSOURCE.VALUE = ALLTRIM(ODBC_SETUP.SQL_SOURCE)
         
         THISFORM.SCREENPASSWORD.VALUE = ALLTRIM(ODBC_SETUP.SCR_PASWRD)
         
         IF USED('ODBC_SETUP')
            SELECT ODBC_SETUP
            USE IN ODBC_SETUP
         ENDIF
      ENDIF
      
   ENDPROC
   
   PROCEDURE CMD_SQLSOURCE.CLICK
      THISFORM.TABLE_SELECT.CLEAR()
      THISFORM.ODBCCONFIG.SET_SOURCE()
      THISFORM.ODBCCONFIG.GET_TABLES()
      THISFORM.SQLSOURCE.VALUE = THISFORM.ODBCCONFIG.SQLSOURCE
      
      FOR N = 1 TO ALEN(THISFORM.ODBCCONFIG.TABLE_NAMES)
         THISFORM.TABLE_SELECT.ADDITEM(THISFORM.ODBCCONFIG.TABLE_NAMES(N))
      ENDFOR
      
      THISFORM.TABLE_SELECT.VALUE = THISFORM.ODBCCONFIG.TABLE_NAMES(1)
      THISFORM.TABLE_SELECT.InteractiveChange
   ENDPROC
      
   *PROCEDURE SQLSOURCE.InteractiveChange
   *   THISFORM.ODBCCONFIG.SET_SOURCE(THIS.VALUE)
   *   FOR EACH TABLENAME IN THISFORM.ODBCCONFIG.TABLE_NAMES
   *      IF TABLENAME != .F.
   *         THISFORM.TABLE_SELECT.ADDITEM(TABLENAME)
   *      ENDIF
   *   ENDFOR
   *ENDPROC
      
   PROCEDURE TABLE_SELECT.InteractiveChange
      THISFORM.FIELD_LIST.CLEAR()
      THISFORM.ODBCCONFIG.ODBCTABLE = THIS.VALUE
      THISFORM.ODBCCONFIG.GET_FIELDS()
      FOR EACH FIELDNAME IN THISFORM.ODBCCONFIG.FIELD_NAMES
         THISFORM.field_list.ADDITEM(FIELDNAME)
      ENDFOR
   ENDPROC
   
   PROCEDURE CMD_SAVE.CLICK
      IF FILE('ODBC_SETUP.DBF')
         USE ODBC_SETUP IN 0 SHARED
         SELECT ODBC_SETUP
         
         REPLACE ODBC_SETUP.SQL_LOGIN WITH ALLTRIM(THISFORM.SQLLOGIN.VALUE)
         REPLACE ODBC_SETUP.SQL_PASWRD WITH ALLTRIM(THISFORM.SQLPASSWORD.VALUE)
         REPLACE ODBC_SETUP.SQL_SOURCE WITH ALLTRIM(THISFORM.SQLSOURCE.VALUE)
         REPLACE ODBC_SETUP.SCR_PASWRD WITH ALLTRIM(THISFORM.SCREENPASSWORD.VALUE)
         
         IF USED('ODBC_SETUP')
            SELECT ODBC_SETUP
            USE IN ODBC_SETUP
         ENDIF
      ENDIF
      
      THISFORM.RELEASE
   ENDPROC

   PROCEDURE CMD_CANCEL.CLICK
      THISFORM.RELEASE
   ENDPROC
   
ENDDEFINE

DEFINE CLASS PASSWORDSCR AS MODALSCR
   Height = 170
   Width = 200
   Caption = "Password"
   Password = ''
   Success = .F.
   Trys = 0
   
   ADD OBJECT LB_PASS AS STDLABEL WITH Left = 10, Top = 10, Caption = "",;
       Forecolor = RGB(0,0,128), Width = 180, Height = 60, Wordwrap = .T.,;
       Alignment = 2
       LB_PASS.CAPTION = "Enter Password to Proceed into Interface Setup"
      
   ADD OBJECT PASS_WORD AS LIGHTUPTEXTBOX WITH Left = 50, Top = 90
       PASS_WORD.Width = 100
       PASS_WORD.FontSize = 12
       PASS_WORD.PasswordChar = '*'
   
   ADD OBJECT CMD_OK AS STDBUTTON WITH Left = 60, Top = 138, Caption = "\<OK"
       CMD_OK.ToolTipText = 'OK'
       CMD_OK.Default = .T.
   
   PROCEDURE LOAD
      =CAPSLOCK(.F.)
   ENDPROC
   
   PROCEDURE DESTROY
      =CAPSLOCK(.T.)
   ENDPROC
   
   PROCEDURE CMD_OK.Click
      THISFORM.Trys = THISFORM.Trys + 1
      IF ALLTRIM(THISFORM.PASS_WORD.VALUE) == THISFORM.PASSWORD
         THISFORM.Success = .T.
         THISFORM.Hide()
      ELSE
         IF THISFORM.Trys < 3
            =MESSAGEBOX('Password To Interface Setup is Incorrect! Try Again!',16,'')
            THISFORM.PASS_WORD.SetFocus()
         ELSE
            =MESSAGEBOX('Password To Interface Setup is Incorrect! Must Exit!',16,'')
            THISFORM.Hide()
         ENDIF
      ENDIF
   ENDPROC
   
ENDDEFINE